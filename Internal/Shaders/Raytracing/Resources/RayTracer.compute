// Each #kernel tells which function to compile; you can have many kernels

#pragma kernel RayTrace
#pragma kernel InitializeRays
#include "../../ShaderHelpers.hlsl"

RWTexture2D<float4> _RayTracer;
float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;
Texture2D<float4> _SkyBoxTexture;
SamplerState sampler_SkyBoxTexture;
float epsilon = 0.00001;
int _MaxBounces;
int _MinBounces;
float _Samples;
float2 _FrameSeed;
float _RussianRouletteChance;

struct Ray
{
	float3 o;
	float3 d;
	float3 color;
	float3 energy;
	int bounces;
};


struct MeshObject
{
	float4x4 localToWorld;
	int indicesOffset;
	int indicesCount;
	float3 position;
	float3 AABBMin;
	float3 AABBMax;
	float3 color;
	float emission;
	float smoothness;
	float transparency;
	uint id;
};

struct Vertex
{
	float3 position;
	float3 normal;
	float2 uv;
};

struct Hit
{
	float3 position;
	float distance;
	float3 normal;
	float3 color;
	float2 uv;
	Vertex vertices[3];
	MeshObject meshObj;

};

StructuredBuffer<MeshObject> _MeshObjects;
StructuredBuffer<Vertex> _Vertices;
StructuredBuffer<int> _Indices;
RWStructuredBuffer<Ray> _Rays;
uint numOfStructs, sizeOfStrides;

Vertex CreateVertex(inout Vertex v)
{
	v.position = float3(0, 0, 0);
	v.normal = float3(0, 0, 0);
	v.uv = float2(0, 0);
	return v;
}

Ray CreateRay(float3 og, float3 dir, float2 pixel)
{
	Ray ray;
	ray.o = og;
	ray.d = dir;
	ray.color = float3(1, 1, 1);
	ray.energy = float3(0, 0, 0);
	ray.bounces = 0;
	return ray;
}

Hit CreateHit()
{
	Hit hit;
	hit.normal = 0;
	hit.position = 0;
	hit.distance = 1500;
	hit.color = float3(0.2, 0.6, 0.4);
	Vertex v0, v1, v2;
	CreateVertex(v0);
	CreateVertex(v1);
	CreateVertex(v2);
	hit.vertices[0] = v0;
	hit.vertices[1] = v1;
	hit.vertices[2] = v2;

	hit.uv = 0;
	hit.meshObj = _MeshObjects[0];

	return hit;
}

Ray CreateCameraRay(float2 uv)
{
	//Get the world position of the origin, which is basically just the camera.
	// Last column contains position, take that.
	float3 og = mul(_CameraToWorld, float4(0, 0, 0, 1)).xyz;

	//The scene has a depth to it. The project matrix takes the depth and places it flat onto a 2D plane.
	//The inverse projection does this process backwards. By multiplying it we go backwards.
	float3 dir = mul(_CameraInverseProjection, float4(uv, -1, 1)).xyz;
	dir = mul(_CameraToWorld, float4(dir, 0)).xyz; //Take this 3D pixel point and make it world position.
	dir = normalize(dir);
	return CreateRay(og, dir, uv);
}

float4 SampleSkyBox(inout Ray ray)
{
	float3 dir = normalize(ray.d);
	float u = 0.5 + atan2(dir.z, dir.x) / (2 * UNITY_PI);
	float v = 0.5 - asin(-dir.y) / UNITY_PI;
	float4 skyTex = _SkyBoxTexture.SampleLevel(sampler_SkyBoxTexture, float2(u, v), 0);
	ray.color = skyTex.xyz;
	ray.energy = ray.color;
	return 1;
}

Hit Shade(inout Hit hit, inout Ray ray, float3 normal)
{
	float3 lightDir = normalize(_WorldSpaceLightPos0);
	float NdotL = clamp(dot(normal, lightDir), 0.0825, 1.0);
	hit.color = NdotL * _LightColor0.xyz;
	return hit;
}

float3 Reflection(inout Ray ray, inout Hit hit)
{
	if (hit.distance < 149)
	{
		float3 spec = float3(0.6, 0.6, 0.6);
		ray.o = hit.position + hit.normal * 0.001;
		
		ray.d = reflect(ray.d, hit.normal);
		ray.energy *= spec;
		hit.distance = 9999999;
		hit.color *= ray.energy;
		return 1;
	}
	else
	{
		hit.color = SampleSkyBox(ray).xyz*ray.energy;
		ray.energy = 0;
		return SampleSkyBox(ray).xyz;
	}
}

void IntersectPlane(inout Hit hit, in Ray ray, float3 a, float3 b, float3 c)
{

	float3 n = GetTriangleNormal(a, b, c);

	float3 center = GetTriangleCenter(a, b, c);
	float t = dot(center - ray.o, n) / dot(ray.d, n);
	if (t > epsilon && t < hit.distance)
	{
		hit.normal = n;
		hit.distance = t;
		hit.position = ray.o + t * ray.d;
		Shade(hit, ray, n);
		hit.color *= 20/t;
	}
}

//Moller method.
bool TriangleTrace(Ray ray, float3 a, float3 b, float3 c, inout float t, inout float u, inout float v)
{
	float3 edge1 = b - a;
	float3 edge2 = c - a;
	
	float3 pvec = cross(ray.d, edge2);
	
	float determinate = dot(edge1, pvec);

	if (determinate < epsilon)
		return false;
	
	float3 tvec = ray.o - a;
	float inv_det = 1.0 / determinate;
	
	u = dot(tvec, pvec) * inv_det;
	if (u < 0.0 || u > 1.0)
	{
		return false;
	}
	
	float3 qvec = cross(tvec, edge1);
	v = dot(ray.d, qvec) * inv_det;
	if (v < 0.0 || u + v > 1.0)
	{
		return false;
	}

	//Final calculate of T after crammer's rule.
	t = dot(edge2, qvec) * inv_det;
	
	return true;
}


//Ray slabs test for AABB.
bool TraceAABB(inout Ray ray, inout Hit hit, MeshObject meshObject)
{
	float3 invRayDir = 1.0 / ray.d;
	float3 tLower = (meshObject.AABBMin - ray.o) * invRayDir;
	float3 tUpper = (meshObject.AABBMax - ray.o) * invRayDir;

	float4 tMin = float4(min(tLower, tUpper), 0);
	float4 tMax = float4(max(tLower, tUpper), 10000000);

	//get max component of tMin.
	float tBoxMin = max(max(tMin.x, tMin.y), tMin.z);
	//get min component of tMax.
	float tBoxMax = min(min(tMax.x, tMax.y), tMax.z);

	return tBoxMin <= tBoxMax;
}

bool TraceMesh(inout Ray ray, inout Hit hit, MeshObject meshObject)
{
	uint offset = meshObject.indicesOffset;
	uint count = offset + meshObject.indicesCount;
	float3 tuv = 0;

	for (uint i = offset; i < count; i += 3)
	{
		float3 vp0 = (mul(meshObject.localToWorld, float4(_Vertices[_Indices[i]].position, 1))).xyz;
		float3 vp1 = (mul(meshObject.localToWorld, float4(_Vertices[_Indices[i + 1]].position, 1))).xyz;
		float3 vp2 = (mul(meshObject.localToWorld, float4(_Vertices[_Indices[i + 2]].position, 1))).xyz;

		if (TriangleTrace(ray, vp0, vp1, vp2, tuv.x, tuv.y, tuv.z))
		{
			float t = tuv.x;
			if (t > 0 && t < hit.distance)
			{
				//float3 PointPosition = (mul(meshObject.localToWorld, float4(ray.o + t * ray.d, 1))).xyz;
				//Set the vertices
				hit.vertices[0] = _Vertices[_Indices[i]];
				hit.vertices[1] = _Vertices[_Indices[i + 1]];
				hit.vertices[2] = _Vertices[_Indices[i + 2]];
				hit.distance = t;
				hit.uv = float2(tuv.y, tuv.z);
				hit.position = ray.o + t * ray.d;
				hit.meshObj = meshObject;
				return true;
			}
		}
	}
	return false;
}

void SceneTrace(inout Ray ray, inout Hit hit)
{
	for (int k = 0; k < numOfStructs; k++)
	{
		MeshObject meshObject = _MeshObjects[k];
		if (TraceAABB(ray, hit, meshObject) == true)
		{
			TraceMesh(ray, hit, meshObject);
		}
	}
	
	MeshObject meshObject = hit.meshObj;
	Vertex v0 = hit.vertices[0];
	Vertex v1 = hit.vertices[1];
	Vertex v2 = hit.vertices[2];
	//Convert to world space along with ray and compute the barycentric coordinate.
	v0.position = (mul(meshObject.localToWorld, float4(v0.position, 1))).xyz;
	v1.position = (mul(meshObject.localToWorld, float4(v1.position, 1))).xyz;
	v2.position = (mul(meshObject.localToWorld, float4(v2.position, 1))).xyz;
	v0.normal = (mul(meshObject.localToWorld, float4(v0.normal, 0))).xyz;
	v1.normal = (mul(meshObject.localToWorld, float4(v1.normal, 0))).xyz;
	v2.normal = (mul(meshObject.localToWorld, float4(v2.normal, 0))).xyz;
	float3 barycentric = Barycentric(v0.position, v1.position, v2.position, hit.position);

	//Now that we have the barycentric coordinate we can use this to reconstruct the UVs.
	float2 uvs = v0.uv * barycentric.x + v1.uv * barycentric.y + v2.uv * barycentric.z;

	float3 normals = normalize(v0.normal * barycentric.x + v1.normal * barycentric.y + v2.normal * barycentric.z);
	
	hit.normal = normals;
	hit.uv = uvs;
}

Hit IntersectSphere(inout Hit hit, in Ray ray, float radius, float3 center)
{
	//Quadratic formula.
	float a = dot(ray.d, ray.d);
	float b = 2 * dot((ray.o - center), ray.d);
	float c = dot(ray.o - center, ray.o - center) - radius * radius;
	float d = b * b - 4 * a * c;

	float t1 = (-b + sqrt(b * b - 4 * a * c) / 2 * a);
	float t2 = (-b - sqrt(b * b - 4 * a * c) / 2 * a);

	if(d < 0)
		return hit;
	if (t1 > epsilon && t1 < hit.distance && t1 < t2)
	{
		hit.position = ray.o + t1 * ray.d;

		float3 n = GetSphereNormal(hit.position, radius);
		hit.normal = n;
		hit.distance = t1;
		Shade(hit, ray, n);
		return hit;
	}
	else if (t2 > epsilon && t2 < hit.distance && t2 < t1)
	{
		hit.position = ray.o + t2 * ray.d;
		float3 n = GetSphereNormal(hit.position, radius);
		hit.normal = n;
		hit.distance = t2;
		Shade(hit, ray, n);
	}
	return hit;
}

void Trace(inout Ray ray, inout Hit hit)
{
	float3 a = float3(1, -3, 0);
	float3 b = float3(-1, -3, 0);
	float3 c = float3(0,-3,0.5);
	
	float3 center = float3(0, 0, 0);
	IntersectPlane(hit, ray, a, b, c);
	IntersectSphere(hit, ray, 1, center);
	for (int i = -5; i < 5; i++)
	{
		for (int j = -5; j < 5; j++)
		{
			float3 center = float3(i*2, 0, j*2);
			IntersectPlane(hit, ray, a, b, c);
			IntersectSphere(hit, ray, 1, center);
		}
	}
}

void LambertDiffuse(inout Ray ray, inout Hit hit, float2 pixel, float2 seed)
{
	ray.o = hit.position + hit.normal * 0.001;
	ray.d = RandomPointOnHemisphere(pixel, hit.normal, seed, 1.0, 0.25);
	//ray.d = reflect(ray.d, hit.normal);

	//Get next event estimator.
	float maxEmission = 0;
	Ray lightRay;
	float3 omega = 0;
	uint maxEmissionID = -1;
	MeshObject lightSource;
	for (int i = 0; i < _MeshObjects.Length; i++)
	{
		MeshObject meshObj = _MeshObjects[i];
		if (meshObj.emission > maxEmission)
		{
			maxEmission = meshObj.emission;
			maxEmissionID = meshObj.id;
			lightSource = meshObj;
		}
	}

	float2 xy = randGaussian(float3(pixel + seed, seed.y), rand(seed.x));
	float2 xz = randGaussian(float3(pixel + seed + 2452, seed.x), rand(seed.y));
	float3 uv = float3(xy, xz.x);
	float3 area = float3(1.15,0.015,1.15);
	float3 lightSample = (uv*area) + lightSource.position;
	omega = normalize(lightSample - hit.position);
	lightRay = CreateRay(hit.position, omega, pixel);

	float v = 0;
	Hit lightHit = CreateHit();
	SceneTrace(lightRay, lightHit);

	
	if (lightHit.meshObj.id == maxEmissionID)
		v = 1;

	float ps = 1 / (2 * UNITY_PI); //Probability of a random point on a hemisphere.
	float p = dot(lightHit.normal, -omega) * (1 / dot(lightSample - hit.position, lightSample - hit.position));
	float diffuse = sdot(hit.normal, ray.d);
	//float specular = 
	float cos_theta_omega = sdot(hit.normal, omega);
	float weight = ray.bounces == 0 ? 1.0 : 0.5;
	float directLightWeight = ray.bounces == 0 ? 1.0 : 0.0;
	ray.color *= 2 * hit.meshObj.color;
	//ray.energy += ray.color;
	ray.energy += lightHit.meshObj.emission * ray.color * v *cos_theta_omega * p; //* (1.0 / (1.0 - _RussianRouletteChance));
	ray.energy += hit.meshObj.emission * ray.color * diffuse * ps * directLightWeight;
	ray.energy *= (1.0 / (1.0 -_RussianRouletteChance)) * weight;
}

void ExecuteShadingFunction(inout Ray ray, inout Hit hit, float2 pixel, float2 seed)
{	
	//Lambert Diffuse shading.
	LambertDiffuse(ray, hit, pixel, seed);
	
}


[numthreads(8, 8, 1)]
void InitializeRays(uint3 id : SV_DispatchThreadID)
{
	uint width, height;
	
	_RayTracer.GetDimensions(width, height);


	//This is a ray for each pixel and in the pixel's center.
	//So id.xy is this pixel, then offsetted to center, then divided by the width and height to get said center.
	//Then range is shifted via 2 - 1.
	float2 pixel = ((id.xy + float2(0.5, 0.5)) / float2(width, height)) * 2 - 1; //Note in HLSL this vector v - scalar is applied to each element in v.
	Ray ray = CreateCameraRay(pixel);
	_Rays[id.x + id.y * width] = ray;
}

[numthreads(8,8,1)]
void RayTrace(uint3 id : SV_DispatchThreadID)
{
	

	float2 seed = _FrameSeed + id.z;
	Hit hit = CreateHit();
	
	uint width, height;
	_MeshObjects.GetDimensions(numOfStructs, sizeOfStrides);

	_RayTracer.GetDimensions(width, height);
	Ray ray = _Rays[id.x + id.y * width];
	if(ray.bounces > 999999)
		return;



	//This is a ray for each pixel and in the pixel's center.
	//So id.xy is this pixel, then offsetted to center, then divided by the width and height to get said center.
	//Then range is shifted via 2 - 1.
	float2 pixel = ((id.xy + float2(0.5, 0.5)) / float2(width, height)) * 2 - 1; //Note in HLSL this vector v - scalar is applied to each element in v.
	/*
	while (ray.bounces < 5) {
	
	//Perform a ray trace.
	SceneTrace(ray, hit);
	ExecuteShadingFunction(ray, hit, pixel, seed + ray.bounces * (id.x + id.y * width));
	
	ray.bounces++;


	
		//Terminate ray.
		if ( (randGaussian(float3(id.x, id.y, _FrameSeed.y) + _Time.xyz, _FrameSeed.x).x >= _RussianRouletteChance) && ray.bounces > 2)
		{
		
			_RayTracer[id.xy] += float4(ray.energy, 1.0) * (1.0 / _Samples);
			ray = CreateCameraRay(pixel);
			return;
		}
	}
	//_RayTracer[id.xy] += float4(ray.energy, 1.0) * (1.0 / _Samples);
	_Rays[id.x + id.y * width] = ray;
	*/
	//Perform a ray trace.
	SceneTrace(ray, hit);
	ExecuteShadingFunction(ray, hit, pixel, seed + ray.bounces * (id.x + id.y * width));

	ray.bounces++;
	if ( 
		((randGaussian(float3(id.x + _FrameSeed.x + _FrameSeed.y, _FrameSeed.y + id.x + id.y * width, _FrameSeed.y) + _Time.xyz, _FrameSeed.y).x >= _RussianRouletteChance) || ray.bounces > _MaxBounces) &&
			ray.bounces > _MinBounces)
	{
		_RayTracer[id.xy] += float4(ray.energy, 1.0) * (1.0 / _Samples);
		ray = CreateCameraRay(pixel);
		//return;
	}
	else if(ray.bounces < _MinBounces)
	{
		_RayTracer[id.xy] += float4(ray.energy, 1.0) * (1.0 / _Samples);
	}

	//_RayTracer[id.xy] += float4(ray.energy, 1.0) * (1.0 / _Samples);
	_Rays[id.x + id.y * width] = ray;
}
